-- programs ------------------------------------------------

entrypoints Program ;

Prog.      Program ::= [TopDef] ;

separator  nonempty TopDef "" ;

-- functions -----------------------------------------------

TopFunDef. TopDef ::= FunDef ;

FnDef.     FunDef ::= Type Ident "(" [Arg] ")" Block ;

Ar.        Arg ::= Type Ident ;

separator  Arg "," ;

-- classes -------------------------------------------------

ClDef.     TopDef ::= "class" Ident "{" [ClMem] "}" ;

ClExtDef.  TopDef ::= "class" Ident "extends" Ident "{" [ClMem] "}" ;

ClAtr.     ClMem ::= Type Ident ";" ;

ClMeth.    ClMem ::= FunDef ;

separator  ClMem "" ;

-- statements ----------------------------------------------

Blck.      Block ::= "{" [Stmt] "}" ;

separator  Stmt "" ;

Empty.     Stmt ::= ";" ;

BStmt.     Stmt ::= Block ;

Decl.      Stmt ::= Type [Item] ";" ;

NoInit.    Item ::= Ident ; 

Init.      Item ::= Ident "=" Expr ;

separator  nonempty Item "," ;

Ass.       Stmt ::= LVal "=" Expr  ";" ;

Incr.      Stmt ::= LVal "++"  ";" ;

Decr.      Stmt ::= LVal "--"  ";" ;

Ret.       Stmt ::= "return" Expr ";" ;

VRet.      Stmt ::= "return" ";" ;

Cond.      Stmt ::= "if" "(" Expr ")" Stmt  ;

CondElse.  Stmt ::= "if" "(" Expr ")" Stmt "else" Stmt  ;

While.     Stmt ::= "while" "(" Expr ")" Stmt ;

ForEach.   Stmt ::= "for" "(" Type Ident ":" Expr ")" Stmt ;

SExp.      Stmt ::= Expr  ";" ;

-- lvalues -------------------------------------------------

LVar.      LVal ::= Ident ;

LArr.      LVal ::= Ident "[" Expr "]" ;

LMem.      LVal ::= Ident "." Ident ;

-- types ---------------------------------------------------

Int.       Type ::= "int" ;

Str.       Type ::= "string" ;

Bool.      Type ::= "boolean" ;

Void.      Type ::= "void" ;

Obj.       Type ::= Ident ;

Arr.       Type ::= Type "[]" ;

internal   Fun. Type ::= Type "(" [Type] ")" ;

separator  Type "," ;

-- expressions ---------------------------------------------

EVar.      Expr6 ::= LVal ;

ELitInt.   Expr6 ::= Integer ;

ELitTrue.  Expr6 ::= "true" ;

ELitFalse. Expr6 ::= "false" ;

EApp.      Expr6 ::= LVal "(" [Expr] ")" ;

EString.   Expr6 ::= String ;

Neg.       Expr5 ::= "-" Expr6 ;

Not.       Expr5 ::= "!" Expr6 ;

EMul.      Expr4 ::= Expr4 MulOp Expr5 ;

EAdd.      Expr3 ::= Expr3 AddOp Expr4 ;

ERel.      Expr2 ::= Expr2 RelOp Expr3 ;

EAnd.      Expr1 ::= Expr2 "&&" Expr1 ;

EOr.       Expr ::= Expr1 "||" Expr ;

ENewObj.   Expr6 ::= "new" Ident ;

ENewArr.   Expr6 ::= "new" Type "[" Expr "]" ;

ECastNull. Expr6 ::= "(" Type ")" "null" ;

coercions  Expr 6 ;

separator  Expr "," ;

-- operators -----------------------------------------------

Plus.      AddOp ::= "+" ;

Minus.     AddOp ::= "-" ;

Times.     MulOp ::= "*" ;

Div.       MulOp ::= "/" ;

Mod.       MulOp ::= "%" ;

LTH.       RelOp ::= "<" ;

LE.        RelOp ::= "<=" ;

GTH.       RelOp ::= ">" ;

GE.        RelOp ::= ">=" ;

EQU.       RelOp ::= "==" ;

NE.        RelOp ::= "!=" ;

-- comments ------------------------------------------------

comment    "#" ;

comment    "//" ;

comment    "/*" "*/" ;
